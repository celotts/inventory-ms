plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'jacoco'
    id 'com.github.ben-manes.versions' version '0.53.0'
    id 'org.owasp.dependencycheck' version '9.0.7'
}

dependencies {
    // Core Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Security (opcional)
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // Spring Cloud
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'io.github.resilience4j:resilience4j-spring-boot3'

    // OpenAPI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

    // Commons Lang (recomendado)
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    // DB
    runtimeOnly "org.postgresql:postgresql"
    testImplementation "com.h2database:h2"

    // MapStruct
    implementation "org.mapstruct:mapstruct:1.5.5.Final"
    annotationProcessor "org.mapstruct:mapstruct-processor:1.5.5.Final"
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // DevTools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation platform("org.testcontainers:testcontainers-bom:1.19.8")
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'

    // Mockito / Security Test
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.springframework.security:spring-security-test'

    // Configuration processor
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

// Forzar versiones seguras de dependencias transitivas
configurations.all {
    resolutionStrategy {
        force 'com.google.guava:guava:33.2.0-jre'
    }
}

springBoot {
    mainClass = 'com.celotts.productservice.ProductServiceApplication'
}

bootJar {
    archiveFileName = 'product-service-0.0.1-SNAPSHOT.jar'
}

compileJava {
    options.compilerArgs += [
            '-Amapstruct.defaultComponentModel=spring',
            '-Amapstruct.unmappedTargetPolicy=IGNORE'
    ]
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation']
}

dependencyCheck {
    autoUpdate = false
    failBuildOnCVSS = 0
    nvd {
        skip = true
    }
}
